# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

_gitname=powerline
_gitbranch=develop
pkgname="python2-${_gitname}-fontpatcher-git"
pkgdesc='OTF/TTF font patcher for Powerline symbols'
pkgver=866.db80fc9
pkgrel=1
url='https://github.com/Lokaltog/powerline'
license=('MIT')
arch=('any')
makedepends=('git' 'python2-distribute')
depends=('python2' 'fontforge')
provides=('powerline-fontpatcher')
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}"
	"fontpatcher.py.patch")
sha256sums=('SKIP'
	    '85576097662ab4203968b5fba1d59ec2653a390cdd4db9cee8ffa7bd4c5a7253')

pkgver() {
	cd "${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

prepare() {
	cd "${srcdir}/${_gitname}"
	patch -p1 < ../fontpatcher.py.patch
}

package() {
	cd "${_gitname}"

	# Font patcher
	install -Dm755 "font/fontpatcher.py" "${pkgdir}/usr/bin/powerline-fontpatcher"
	install -Dm644 "font/fontpatcher-symbols.sfd" "${pkgdir}/usr/share/${_gitname}/fontpatcher-symbols.sfd"
}
