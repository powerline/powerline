# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

_gitname=powerline
_gitbranch=develop
pkgname=("python2-${_gitname}-git" "powerline-fontpatcher")
pkgbase=powerline
pkgver=800.c1ae7f3
pkgrel=2
pkgdesc='The ultimate statusline/prompt utility.'
url='https://github.com/Lokaltog/powerline'
license=('MIT')
arch=('any')
depends=('python2>=2.6')
makedepends=('git' 'python2-distribute')
optdepends=('python2-psutil: improved system information'
	'python2-pygit2: improved git support'
	'mercurial: improved mercurial support'
	'zsh: better shell prompt'
	'gvim: vim compiled with Python support')
provides=('powerline')
conflicts=('python-powerline-git')
replaces=('powerline-git')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}"
	"fontpatcher.py.patch"
	"${install}")
sha256sums=('SKIP'
            '85576097662ab4203968b5fba1d59ec2653a390cdd4db9cee8ffa7bd4c5a7253'
            'e8ab7fb51ac7244bfad973a999c9333ba4334fa391aa890489cf8c8f1211c94f')

pkgver() {
	cd "${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

prepare() {
	cd "${srcdir}/${_gitname}"
	patch -p1 < ../fontpatcher.py.patch
}

package_powerline-fontpatcher() {
	pkgdesc='OTF/TTF font patcher for powerline symbols'
	depends=('python2' 'fontforge')

	cd "${_gitname}"

	msg2 "Installing fontpatcher..."
	install -dm755 "${pkgdir}/usr/share/$_gitname"
	install -dm755 "${pkgdir}/usr/bin"
	install -m755 "font/fontpatcher.py" "${pkgdir}/usr/bin/"
	install -m644 "font/fontpatcher-symbols.sfd" "${pkgdir}/usr/share/$_gitname"
}

package_python2-powerline-git() {
	cd "${_gitname}"
	python2 setup.py install --root="${pkgdir}" --optimize=1 || return 1

	msg2 "Installing fonts..."
	install -dm755 "${pkgdir}/usr/share/fonts/OTF/"
	install -dm755 "${pkgdir}/etc/fonts/conf.avail"
	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -m644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -m644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"

	msg2 "Installing vim plugin..."
	install -dm755 "${pkgdir}/usr/share/vim/vimfiles/plugin"
	install -m644 "powerline/bindings/vim/plugin/powerline.vim" "${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim"

	msg2 "Installing zsh plugin..."
	install -dm755 "${pkgdir}/usr/share/zsh/site-contrib"
	install -m644 "powerline/bindings/zsh/powerline.zsh" "${pkgdir}/usr/share/zsh/site-contrib/powerline.zsh"

	msg2 "Installing tmux configuration..."
	install -dm755 "${pkgdir}/usr/share/tmux"
	install -m644 "powerline/bindings/tmux/powerline.conf" "${pkgdir}/usr/share/tmux/powerline.conf"

	msg2 "Installing license..."
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
